plugins {
    id 'java'
    id 'org.springframework.boot' version '3.4.3'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'jacoco'
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

jacoco {
    toolVersion = "0.8.10"  // 최신 버전 사용
}

test {
    useJUnitPlatform()  // JUnit5 사용 설정 (중복이라도 명시적으로 설정)
    finalizedBy jacocoTestReport  // 테스트 후 리포트 생성하도록 설정
    systemProperty "spring.profiles.active", "test"
    testLogging {
        events "passed", "skipped", "failed"
        exceptionFormat "full"
        showExceptions true
        showCauses true
        showStackTraces true
    }
}


jacocoTestReport {
    dependsOn test
    reports {
        html.required.set(true)
        xml.required.set(true)
        csv.required.set(true)
        html.outputLocation.set(file("$buildDir/reports/jacoco"))
    }

    classDirectories.setFrom(
            files(fileTree(
                    dir: "$buildDir/classes/java/main",
                    excludes: [
                            '**/dto/**',
                            '**/type/**',
                            '**/handler/**',
                            '**/util/**'
                    ]
            ))
    )
}

    jacocoTestCoverageVerification {
    violationRules {
        rule {
            element = 'CLASS'

            limit {
                counter = 'INSTRUCTION'
                value = 'COVEREDRATIO'
                minimum = 0.8  // 최소 커버리지 80%로 설정
            }
        }
    }
}
check.dependsOn jacocoTestCoverageVerification

configurations {
    all {
        exclude group: 'commons-logging', module: 'commons-logging'
    }
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

ext {
    set('springCloudVersion', "2024.0.0")
}

dependencies {
    implementation 'org.mybatis.spring.boot:mybatis-spring-boot-starter:3.0.4'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'
    implementation 'org.springframework.boot:spring-boot-starter-data-redis-reactive'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.cloud:spring-cloud-starter-openfeign'
    implementation 'jakarta.persistence:jakarta.persistence-api:3.0.0'
    implementation 'org.springframework.boot:spring-boot-starter-mail'
    implementation 'jakarta.annotation:jakarta.annotation-api:2.1.1'
    implementation 'com.amazonaws:aws-java-sdk-s3:1.12.673'
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.1.0'
    implementation 'org.springframework.cloud:spring-cloud-starter-config'
    implementation 'org.springframework.cloud:spring-cloud-starter-vault-config'
    implementation 'org.springframework.cloud:spring-cloud-starter-bootstrap'
    implementation 'org.hibernate.validator:hibernate-validator:8.0.1.Final'

    implementation 'io.jsonwebtoken:jjwt-api:0.11.5'  // JWT API
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.11.5'    // JWT 구현체
    runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.11.5' // JSON 처리를 위한 Jackson 모듈

    compileOnly 'org.projectlombok:lombok'
    runtimeOnly 'com.mysql:mysql-connector-j'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test:3.4.3'
    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation 'com.h2database:h2'
    testImplementation "org.testcontainers:testcontainers:1.19.0"
    testImplementation "org.testcontainers:junit-jupiter:1.19.0"
    testImplementation 'io.findify:s3mock_2.13:0.2.6'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    testImplementation 'org.assertj:assertj-core:3.26.0'
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}
